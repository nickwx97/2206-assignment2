📦 sockets 🏠
📦 files 🏠

🐇 💾 🍇
	🐇❗ 😠 clientSocket 📞🍇
		😀🔤Sending 404...🔤❗
		🍺 📇🐇📄🔤testhtml/404.html🔤❗ ➡ errpage
		📏errpage❓ ➡ conlen
		🍺 💬 clientSocket 📇🔤HTTP/1.1 404 Not Found
Content-Length: 🧲conlen🧲
Vary: Accept-Encoding
Content-Type: text/html
Connection: close❌r❌n❌r❌n🔤❗ ➕ errpage❗
	🍉
🍉

💭 Simple echo server listening on port 8888
🏁 ➡️ 🔢 🍇

	🕴🐇💻🔤SCRIPT=$(pwd); echo -n $SCRIPT > currentdir; chmod 600 currentdir🔤 ❗
	😀🔤Created Dir file🔤❗

	📇🔤/🔤❗➡🖍🆕dirbytes
	🆗tempdirbytes 📇🐇📄🔤currentdir🔤❗ 🍇
		tempdirbytes➡🖍dirbytes
		😀🍺🔡dirbytes❗❗
	🍉
	🙅‍♀️error🍇
		😀🔤🤯Error: 🧲💬error❗🧲🔤❗
		🚪🐇💻-1❗
	🍉

	😀🍺🔡dirbytes❗❗

	💭Starts listening on 8888
	🍺🆕🏄 8888❗️ ➡️ server
	😀🔤Started listener on 8888🔤❗

	🔁👍🍇
		💭 Opens socket with client
		🍺 🙋 server❗️ ➡️ clientSocket
		🛂🆕🧵 🍇 🎍🥡
			🔁👍🍇
				💭Read 8192 bytes (max size for HTTP requests)
				🆗 data 👂 clientSocket 8192❗️ 🍇

					💭 echos client request back
					💭🍺 💬 clientSocket data❗️



					🔡📏data❓❗ ➡ length
					😀🔤length read: 🧲length🧲❌n❌n🔤❗
					😀🍺🔡 data❗️❗️
					↪🍺🔡🔪data 0 3❗❗ 🙌 🔤GET🔤🍇
						😀🔤GET received🔤❗
						🔍data 📇🔤❌r❌n🔤❗ 0❗ ➡ offset
						😀🔡🍺offset❗❗
						🔪data 0 🍺offset❗ ➡ requestline

						🍺🔡requestline❗ ➡ asciirequestline
						😀🔤Request line: 🧲asciirequestline🧲🔤❗

						🔫asciirequestline 🔤 🔤❗ ➡ requestlist
						🐽requestlist 1❗ ➡ dir
						😀🔤Requested Directory: 🧲dir🧲🔤❗

						↪⛳dir 🔤/🔤❗️🍇
							😀🔤Ends with /🔤❗
							💭 return 404, then exit thread(?)
							😠🐇💾 clientSocket❗

							😀🔤Closing socket🔤❗
							🚪clientSocket❗️
							😀🔤End of thread🔤❗
							↩↩
						🍉
						dirbytes ➕ 📇dir❗ ➡fp
						😀🔤❌n❌n🧲🍺🔡fp❗🧲❌n❌n🔤❗

						🆗 readFile 📇🐇📄 🍺🔡fp❗ ❗ 🍇
							💭 return data to client, insert http 200 first
							😀🔤Sending data to client!🔤❗
							📏readFile❓➡conlen
							🍺 💬 clientSocket 📇🔤HTTP/1.1 200 OK
Content-Length: 🧲conlen🧲
Vary: Accept-Encoding
Content-Type: text/html
Connection: close❌r❌n❌r❌n🔤❗ ➕ readFile❗️
						🍉
						🙅‍♀️ error 🍇
							😀🔤🤯Error: 🧲💬error❗🧲🔤❗
							💭 return 404, then exit thread(?)
							😠🐇💾 clientSocket❗

							😀🔤Closing socket🔤❗
							🚪clientSocket❗️
							😀🔤End of thread🔤❗
							↩↩
						🍉

					🍉
					↪❎🤜length 🙌 🔤0🔤 🤛❗🍇
						😀🔤End of thread🔤❗
						↩↩
					🍉
				🍉 🙅‍♀️ error 🍇
					😀🔤🤯Error: 🧲💬error❗🧲🔤❗
				🍉
			🍉
			😀🔤Closing socket🔤❗
			🚪clientSocket❗️
		🍉❗❗
	🍉
	😀🔤End of server code❗❗❗🔤❗
	↩ 0
🍉
