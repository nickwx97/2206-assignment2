📦 sockets 🏠
📦 files 🏠

🐇 🕸 🍇

	🐇❗ 📛 ➡ 🔡🍇
		↩ 🔤emojicode-httpd🔤 💭server name
	🍉

	🐇❗ 👹 ➡ 🔡🍇
		↩ 🔤testhtml/404.html🔤 💭fancy 404
	🍉

	🐇❗ 😎 clientSocket 📞 readFile 📇 🍇
		😀🔤200 OK🔤❗
		📏readFile❓➡conlen
		🍺 💬 clientSocket 📇🔤HTTP/1.1 200 OK❌r❌nServer: 🧲📛❗🧲❌r❌nContent-Length: 🧲conlen🧲❌r❌nCache-Control: max-age=3600❌r❌nContent-Type: text/html❌r❌nConnection: close❌r❌n❌r❌n🔤❗ ➕ readFile❗️
	🍉

	🐇❗ 😠 clientSocket 📞🍇
		😀🔤404 Not Found🔤❗
		🆗 errpage 📇🐇📄👹❗❗ 🍇
			📏errpage❓ ➡ conlen
			🍺 💬 clientSocket 📇🔤HTTP/1.1 404 Not Found❌r❌nServer: 🧲📛❗🧲❌r❌nContent-Length: 🧲conlen🧲❌r❌nContent-Type: text/html❌r❌nConnection: close❌r❌n❌r❌n🔤❗ ➕ errpage❗
		🍉
		🙅‍♀️ error 🍇
			😀🔤🤯Error: 🧲💬error❗🧲🔤❗
			🍺 💬 clientSocket 📇🔤HTTP/1.1 404 Not Found❌r❌nServer: 🧲📛❗🧲❌r❌nContent-Length: 49❌r❌nContent-Type: text/html❌r❌nConnection: close❌r❌n❌r❌nUhh... 404 page... 404? That's even more awkward.🔤❗❗
		🍉
	🍉

	🐇❗ 🤔 clientSocket 📞 path 🔡 host🔡🍇
		😀🔤301 Moved Permanently🔤❗
		🍺 💬 clientSocket 📇🔤HTTP/1.1 301 Moved Permanently❌r❌nServer: 🧲📛❗🧲❌r❌nLocation: http://🧲host🧲🧲path🧲index.html❌r❌n❌r❌n🔤❗❗
	🍉

	🐇❗ 🏥 data 📇 ➡ 🔡🍇
		↩🐽🔫🐽🔫🍺🔡data❗ 🔤Host: 🔤❗ 1❗ 🔤❌r❌n🔤❗ 0❗
	🍉
🍉

💭 Simple HTTP server listening on port 8888
🏁 ➡️ 🔢 🍇

	🕴🐇💻🔤SCRIPT=$(pwd); echo -n $SCRIPT > currentdir; chmod 600 currentdir🔤 ❗
	📇🔤/🔤❗➡🖍🆕dirbytes
	🆗tempdirbytes 📇🐇📄🔤currentdir🔤❗ 🍇
		tempdirbytes➡🖍dirbytes
	🍉
	🙅‍♀️error🍇
		😀🔤🤯Error: 🧲💬error❗🧲🔤❗
		🚪🐇💻-1❗
	🍉
	🍺🆕🏄 8888❗️ ➡️ server
	😀🔤🧲📛🐇🕸❗🧲 Started❌n🔤❗
	😀🔤****************************🔤❗
	🔁👍🍇
		🍺 🙋 server❗️ ➡️ clientSocket
		🛂🆕🧵 🍇 🎍🥡
			🔁👍🍇
				🆗 data 👂 clientSocket 8192❗️ 🍇 💭Read 8192 bytes (max size for HTTP requests)
					🔡📏data❓❗ ➡ length
					😀🍺🔡 data❗️❗️
					↪🍺🔡🔪data 0 3❗❗ 🙌 🔤GET🔤🍇 💭Process GET
						🔍data 📇🔤❌r❌n🔤❗ 0❗ ➡ offset
						🔪data 0 🍺offset❗ ➡ requestline
						🍺🔡requestline❗ ➡ asciirequestline
						🔫asciirequestline 🔤 🔤❗ ➡ requestlist
						🐽requestlist 1❗ ➡ dir
						↪⛳dir 🔤/🔤❗️🍇
							🏥🐇🕸 data ❗ ➡ host
							🤔🐇🕸 clientSocket dir host❗

							🚪clientSocket❗️
							😀🔤****************************🔤❗
							↩↩
						🍉
						dirbytes ➕ 📇dir❗ ➡fp
						🆗 readFile 📇🐇📄 🍺🔡fp❗ ❗ 🍇
							😎🐇🕸 clientSocket readFile ❗
						🍉
						🙅‍♀️ error 🍇
							😀🔤🤯Error: 🧲💬error❗🧲🔤❗
							↪💬error❗🙌🔤Is a directory🔤🍇
								🏥🐇🕸 data ❗ ➡ host
								🤔🐇🕸 clientSocket 🔤🧲dir🧲/🔤 host ❗

								🚪clientSocket❗️
								😀🔤****************************🔤❗
								↩↩
							🍉
							😠🐇🕸 clientSocket❗

							🚪clientSocket❗️
							😀🔤****************************🔤❗
							↩↩
						🍉
					🍉
					↪❎🤜length 🙌 🔤0🔤 🤛❗🍇
						🚪clientSocket❗️
						😀🔤****************************🔤❗
						↩↩
					🍉
				🍉 🙅‍♀️ error 🍇
					😀🔤🤯Error: 🧲💬error❗🧲🔤❗
				🍉
			🍉
			🚪clientSocket❗️
		🍉❗❗
	🍉
	😀🔤End of server code❗❗❗🔤❗
	↩ 0

🍉
